plugins {
  id "java"
  id "groovy"
  id "maven-publish"
  id "com.github.node-gradle.node" version "3.4.0"
}

description = "Tapestry Extensions"
group = "de.eddyson"
version = "0.4.0"

def versions = [
    slf4j: '2.0.13',
    tapestry: '5.9.0',
    selenium: '4.30.0'
]

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation "org.apache.tapestry:tapestry-core-jakarta:$versions.tapestry"

  implementation "org.slf4j:slf4j-api:$versions.slf4j"

  runtimeOnly "com.github.eddyson-de:tapestry-webjars:1.3.0"

  //Webjars
  runtimeOnly('org.webjars.npm:select2:4.0.13') {
    exclude group: 'org.webjars.npm', module: 'almond'
    exclude group: 'org.webjars.npm', module: 'jquery-mousewheel'
  }

  testImplementation "org.apache.tapestry:tapestry-spock:$versions.tapestry"
  testImplementation "org.gebish:geb-spock:6.0"
  testImplementation "com.github.eddyson-de:tapestry-geb:v0.51.0"
  testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:$versions.selenium"
  testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:$versions.selenium"
  testImplementation "io.github.bonigarcia:webdrivermanager:6.3.2"


  // Logback
  testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.6'

  testRuntimeOnly "org.apache.tapestry:tapestry-webresources:$versions.tapestry"
}

test {
  useJUnitPlatform()
}

tasks.withType(Test) {
  systemProperty "geb.env", System.getProperty("geb.env") ?: 'firefox'
  systemProperty "tapestry.service-reloading-enabled", "false"
  systemProperty "tapestry.execution-mode", "test"
  systemProperty "webappLocation", "src/test/webapp"
  systemProperty "jettyPort", 9040
  enableAssertions = true
}

node {
  download = true
  version = "14.20.0"
}

def compiledCoffeeScriptDir = "${project.buildDir}/compiled-coffeescript"

task compileCoffeeScript(type: NpxTask) {
  dependsOn npmInstall

  command = "coffee"
  args = [ "-c", "-o", compiledCoffeeScriptDir, 'src/main/resources/META-INF/modules' ]

  inputs.files fileTree(dir: 'src/main/resources/META-INF/modules', include: '**/*.coffee')
  outputs.dir compiledCoffeeScriptDir

  doFirst {
    delete compiledCoffeeScriptDir
  }
}

processResources {
  dependsOn compileCoffeeScript
}

jar {
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.extensions.modules.ExtensionsModule' }
  rootSpec.exclude 'META-INF/modules/**/*.coffee'
  from(compiledCoffeeScriptDir){ into "META-INF/modules" }
}

task sourceJar(type: Jar) {
  dependsOn classes
  archiveClassifier = "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact tasks.sourceJar
    }
  }
}

task runTestApp(type:JavaExec) {
  mainClass = "org.apache.tapestry5.test.JettyRunner"
  args "-d", "src/test/webapp/", "-p", "9040"
  systemProperty "tapestry.execution-mode", "test"
  classpath = configurations.testRuntimeClasspath + sourceSets.test.output + sourceSets.main.output
}
