plugins {
  id "java"
  id "groovy"
  id "maven-publish"
  id "com.github.node-gradle.node" version "3.1.0"
}

description = "Tapestry Extensions"
group = "de.eddyson"
version = "0.2.0"

def versions = [
    slf4j: '1.7.32',
    tapestry: '5.7.3',
    selenium: '3.141.59'
]

sourceCompatibility = "11"

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation "org.apache.tapestry:tapestry-core:$versions.tapestry"

  implementation "org.slf4j:slf4j-api:$versions.slf4j"

  runtimeOnly "com.github.eddyson-de:tapestry-webjars:1.1.2"

  //Webjars
  runtimeOnly('org.webjars.npm:select2:4.0.13') {
    exclude group: 'org.webjars.npm', module: 'almond'
    exclude group: 'org.webjars.npm', module: 'jquery-mousewheel'
  }

  testImplementation "org.apache.tapestry:tapestry-spock:$versions.tapestry"
  testImplementation "org.gebish:geb-spock:4.1"
  testImplementation "com.github.eddyson-de:tapestry-geb:0.46.0"
  testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:$versions.selenium"
  testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:$versions.selenium"
  testImplementation "io.github.bonigarcia:webdrivermanager:4.4.3"


  // Logback
  testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.5'

  testRuntimeOnly "org.apache.tapestry:tapestry-webresources:$versions.tapestry"
}

tasks.withType(Test) {
  systemProperty "geb.env", System.getProperty("geb.env") ?: 'firefox'
  systemProperty "tapestry.service-reloading-enabled", "false"
  systemProperty "tapestry.execution-mode", "test"
  systemProperty "webappLocation", "src/test/webapp"
  systemProperty "jettyPort", 9040
  enableAssertions = true
}

node {
  download = true
  version = "14.17.3"
}

def compiledCoffeeScriptDir = "${project.buildDir}/compiled-coffeescript"

task compileCoffeeScript(type: NpxTask) {
  dependsOn npmInstall

  command = "coffee"
  args = [ "-c", "-o", compiledCoffeeScriptDir, 'src/main/resources/META-INF/modules' ]

  inputs.files fileTree(dir: 'src/main/resources/META-INF/modules', include: '**/*.coffee')
  outputs.dir compiledCoffeeScriptDir

  doFirst {
    delete compiledCoffeeScriptDir
  }
}

jar {
  dependsOn compileCoffeeScript
  manifest { attributes 'Tapestry-Module-Classes': 'de.eddyson.tapestry.extensions.modules.ExtensionsModule' }
  rootSpec.exclude 'META-INF/modules/**/*.coffee'
  from(compiledCoffeeScriptDir){ into "META-INF/modules" }
}

task sourceJar(type: Jar) {
  dependsOn classes
  classifier "sources"
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact tasks.sourceJar
    }
  }
}

task runTestApp(type:JavaExec) {
  mainClass = "org.apache.tapestry5.test.JettyRunner"
  args "-d", "src/test/webapp/", "-p", "9040"
  systemProperty "tapestry.execution-mode", "test"
  classpath = configurations.testRuntimeClasspath + sourceSets.test.output + sourceSets.main.output
}